buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://dl.bintray.com/palantir/releases/'
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath "com.palantir.baseline:gradle-baseline-java:${baselineVersion}"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:${bintrayPluginVersion}"
        classpath "com.palantir:jacoco-coverage:${jacocoCoverageVersion}"
        classpath "com.palantir.gradle.gitversion:gradle-git-version:${gitVersionVersion}"
        classpath "gradle.plugin.com.palantir.configurationresolver:gradle-configuration-resolver-plugin:${configurationResolverVersion}"
        classpath "gradle.plugin.org.inferred:gradle-processors:1.2.3"
    }
}

apply plugin: 'com.palantir.baseline-config'
apply plugin: 'com.palantir.baseline-idea'
apply plugin: 'com.palantir.git-version'

dependencies {
    // Adds a dependency on the Baseline configuration files. Typically use
    // the same version as the plugin itself.
    baseline "com.palantir.baseline:gradle-baseline-java-config:${baselineVersion}@zip"
}

allprojects {
    group 'com.palantir.tritium'
    version gitVersion()

    apply from: "${rootDir}/gradle/java.gradle"

    configurations {
        testArtifacts.extendsFrom testCompile
    }

    repositories {
        mavenCentral()
        maven {
            url 'https://dl.bintray.com/palantir/releases/'
        }
        maven {
            url 'https://dl.bintray.com/marshallpierce/maven/'
        }
    }

    tasks.withType(Test) {
        testLogging {
            showExceptions true
            exceptionFormat "full"
            showCauses true
            showStackTraces true

            events "started", "passed", "skipped", "failed"
        }
    }
}
