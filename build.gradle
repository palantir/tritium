buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://dl.bintray.com/palantir/releases/'
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'com.netflix.nebula:nebula-publishing-plugin:13.2.0'
        classpath 'com.palantir.baseline:gradle-baseline-java:1.10.1'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:1.9.2'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:0.11.0'
        classpath 'gradle.plugin.org.inferred:gradle-processors:2.2.0'
        classpath 'com.google.errorprone:error_prone_core:2.3.3'
    }
}

apply plugin: 'java'
apply plugin: 'com.palantir.baseline'
apply plugin: 'com.palantir.consistent-versions'
apply plugin: 'com.palantir.git-version'

allprojects {
    group 'com.palantir.tritium'
    version gitVersion()

    apply plugin: 'java-library'
    apply plugin: 'org.inferred.processors'  // installs the "processor" configuration needed for baseline-error-prone
    apply plugin: 'com.palantir.baseline-class-uniqueness'
    apply plugin: 'com.palantir.baseline-exact-dependencies'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    configurations {
        checkstyle {
            resolutionStrategy {
                // checkstyle requires different Guava
                force 'com.google.guava:guava:25.1-jre'
            }
        }
    }

    repositories {
        mavenCentral()
        maven {
            url 'https://dl.bintray.com/palantir/releases/'
        }
        maven {
            url 'https://dl.bintray.com/marshallpierce/maven/'
        }
    }

    dependencies {
        constraints {
            rootConfiguration 'io.dropwizard.metrics:metrics-core', {
                version { strictly '[3, 4[' }
                because "Spark still uses 3.X, which can't co-exist with 4.X"
            }
        }
    }

    plugins.withId('com.palantir.baseline-error-prone', {
        dependencies {
            errorprone 'com.uber.nullaway:nullaway'
        }

        tasks.withType(JavaCompile).configureEach {
            options.compilerArgs += [
                // '-Werror', // disable due to https://github.com/immutables/immutables/issues/291 for now
                '-Xlint:deprecation',
                '-Xlint:unchecked',
            ]
            options.errorprone.disableWarningsInGeneratedCode = true
            options.errorprone.errorproneArgs += [
                '-Werror',
                '-Xlint:deprecation',
                '-Xlint:unchecked',
                '-XepDisableWarningsInGeneratedCode',
                // skipping Immutable* explicitly due to https://github.com/google/error-prone/issues/1165
                '-XepExcludedPaths:.*(generated|Immutable).*',
                '-XepOpt:NullAway:AnnotatedPackages=com.palantir',
            ]

            // warnings not explicitly provided by error-prone
            Set<String> warningsToPromote = [
                'NullAway',
                'Slf4jLogsafeArgs',
                'PreferCollectionTransform',
                'PreferListsPartition',
                'PreferSafeLoggingPreconditions',
                'PreferSafeLoggableExceptions',
                'SwitchStatementDefaultCase',
            ]

            com.google.errorprone.scanner.BuiltInCheckerSuppliers.ENABLED_WARNINGS.forEach {
                check -> warningsToPromote << check.canonicalName()
            }

            com.google.errorprone.scanner.BuiltInCheckerSuppliers.DISABLED_CHECKS.forEach {
                check -> warningsToPromote << check.canonicalName()
            }

            warningsToPromote.removeAll([
                'AndroidJdkLibsChecker', // ignore Android
                'Java7ApiChecker', // tritium requires JDK8+
                'StaticOrDefaultInterfaceMethod', // Android specific
                'Var', // high noise, low signal
            ])

            for (String checkName : warningsToPromote) {
                options.errorprone.errorproneArgs << '-Xep:' + checkName + ':ERROR'
            }

            logger.info("Error-prone args: {}", options.errorprone.errorproneArgs)
        }
    })

    tasks.withType(Test) {
        testLogging {
            showExceptions true
            exceptionFormat "full"
            showCauses true
            showStackTraces true

            events "started", "passed", "skipped", "failed"
        }
    }

    tasks.withType(Javadoc) {
        // suppress Javadoc doclint warnings in Java 8+
        if (!System.getProperty("java.version").startsWith("1.7")) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
    tasks.check.dependsOn(javadoc)

    tasks.check.dependsOn(checkImplicitDependencies)
    tasks.check.dependsOn(checkUnusedDependencies)

}

